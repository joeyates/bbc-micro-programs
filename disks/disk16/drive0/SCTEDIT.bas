
   10  REM DISC EDITOR
   20 
   30 
   40LOMEM=LOMEM+8-(LOMEM MOD8)
   50DIM blc% 256,dir% 256,blk% 12
   60D%=0:rcd%=0
   70rwr%=FALSE
   80hex$="0123456789ABCDEF"
   90TRK%=0:SCT%=0:RSCT%=0
  100fnm$=STRING$(10," ")
  110A$=STRING$(16," ")
  120 
  130MODE5
  140PROCintro
  150MODE0
  160VDU19,0,6;0;19,1,0;0;
  170VDU23,0,10,0,0;0;0;
  180 
  190PRINTTAB(20,2)"D I S C  S E C T O R  E D I T O R"
  200*FX4,1
  210*FX12,4
  220  
  230REPEAT
  240ON ERROR GOTO 400
  250PROCsget
  260PROCread(TRK%,SCT%,blc%)
  270IF rcd%<>0 THEN SOUND1,-15,10,20:PRINTTAB(40,5);"Disc error ";rcd%:GOTO360 ELSE PRINTTAB(40,5);STRING$(20," ")
  280PROCdump
  290PROCedit
  300 PROCwd
  310 IF rwr% THEN PROCwrite(TRK%,SCT%,blc%)
  320VDU28,0,31,79,0
  330IF rwr% AND rcd%<>0 THEN SOUND1,-15,10,20:PRINTTAB(40,5);"Disc error ";rcd%:GOTO 280 ELSE PRINTTAB(40,5);STRING$(20," ")
  340PROCwd
  350IF rwr% THEN PRINT"S e c t o r  r e w r i t t e n":SOUND1,-15,150,5:A=INKEY(300)
  360UNTIL FALSE
  370END
  380  
  390  
  400VDU6:*FX4,0
  410*FX12,0
  420*FX229,0
  430MODE7
  440REPORT:PRINT" AT ";ERL
  450END
  460  
  470DEF PROCwd
  480VDU28,0,31,79,24:CLS
  490ENDPROC
  500  
  510DEF PROCread(trk%,sect%,blc%)
  520blk%?6=&53
  530PROCioset
  540rcd%=blk%?10
  550ENDPROC
  560  
  570DEF PROCwrite(trk%,sect%,blc%)
  580blk%?6=&4B
  590PROCioset
  600rcd%=blk%?10
  610ENDPROC
  620  
  630DEF PROCioset
  640rcd%=0:?blk%=D%:blk%!1=blc%
  650blk%?5=3:blk%?7=trk%
  660blk%?8=sect%:blk%?9=&21
  670X%=blk%MOD256:Y%=blk%DIV256
  680A%=&7F:CALL &FFF1
  690ENDPROC
  700  
  710DEF PROCdump
  720VDU28,0,22,79,6:CLS
  730E%=blc%
  740F%=blc%+256
  750@%=2
  760REPEAT
  770PRINT~E%-blc%;:PRINT"  : ";
  780A$=""
  790FOR X%=E% TO E%+15
  800Y%=?X%
  810IF (Y%<32) OR (Y%>122) THEN A$=A$+"." ELSE A$=A$+CHR$(Y%)
  820IF Y%>15 THEN PRINT;~Y%;" ";ELSE PRINT;"0";~Y%;" ";
  830NEXT
  840PRINT"*"A$"*"
  850E%=E%+16
  860UNTIL E%=F%
  870ENDPROC
  880  
  890DEF PROCsget
  900RSCT%=TRK%*10+SCT%
  910PROCwd
  920PRINT"HIT 'N' FOR NEXT SEQUENTIAL SECTOR, OR"
  930PRINT"TAB TO REQUIRED INPUT FIELD, OVERTYPE AND HIT RETURN"
  940PRINT'"TRACK/SECTOR (DEC)";SPC(4);"RELATIVE SECTOR (HEX)";SPC(11);"FILENAME (DIR.NAME)"
  950PRINTTAB(4);TRK%;","SCT%;TAB(29);~RSCT%;TAB(54);
  960IF LEFT$(fnm$,1)="*" THEN PRINT fnm$ ELSE PRINT STRING$(20," ")
  970VDU11:PRINTTAB(4);:C%=4:fnm$=""
  980A=GET:IF A=9 THEN PRINTTAB((POS+25)MOD75,4);:C%=POS:GOTO 980
  990IF A=13 OR A=27 OR A=127 OR (A>134 AND A<140) THEN 980
 1000IF A=78 OR A=110 THEN RSCT%=RSCT%+1:TRK%=RSCT%DIV10:SCT%=RSCT%MOD10:GOTO 1060
 1010A$=CHR$(A):PRINT A$;SPC(10);STRING$(10,CHR$(8));
 1020INPUT LINE""ASK$:A$=A$+ASK$
 1030ok=FALSE
 1040IF C%=54 THEN PROCipfn ELSE IF C%=29 THEN PROCiprl ELSE PROCipts
 1050IF NOT ok THEN VDU7:GOTO 910
 1060VDU26:PRINTTAB(0,5)"DRIVE:";D%;SPC(3)"TRACK:";TRK%;SPC(3);"SECTOR:";SCT%;SPC(4)
 1070ENDPROC
 1080 
 1090DEF PROCipfn
 1100fnm$=A$
 1110IF LEN(A$)<3 OR MID$(A$,2,1)<>"." THEN fnm$="*INVALID*":ENDPROC
 1120PROCread(0,0,dir%)
 1130DIR$=LEFT$(A$,1):F$=MID$(A$,3,7):IF LEN(F$)<>7 THEN F$=F$+LEFT$(STRING$(6," "),7-LEN(F$))
 1140off=0
 1150FOR I%=dir%+8 TO dir%+248 STEP 8
 1160svd=I%?7:I%?7=&0D:fnt$=$I%:I%?7=svd
 1170IF (F$=fnt$) AND (DIR$=CHR$(svd)) THEN off=I%-dir%:I%=99999
 1180NEXT I%
 1190IF off=0 THEN fnm$="*NOT FOUND*":ENDPROC
 1200PROCread(0,1,dir%)
 1210I=dir%+off+6
 1220RSCT%=(?I AND &01)*256+ ?(I+1)
 1230TRK%=RSCT% DIV10
 1240SCT%=RSCT% MOD10
 1250ok=TRUE
 1260ENDPROC
 1270  
 1280DEF PROCiprl
 1290RSCT%=EVAL("&"+A$)
 1300TRK%=RSCT% DIV 10
 1310SCT%=RSCT% MOD10
 1320ok=TRUE
 1330ENDPROC
 1340 
 1350DEF PROCipts
 1360A$=A$+CHR$13
 1370FOR I%=1 TO LEN(A$):A%=138:X%=0:Y%=ASC(MID$(A$,I%,1)):CALL &FFF4:NEXT
 1380PRINTTAB(4,4);:INPUT""TRK%,SCT%
 1390ok=TRUE
 1400ENDPROC
 1410  
 1420DEF PROCedit
 1430PROCwd
 1440PRINT"EDIT AS REQUIRED. COPY TO REWRITE SECTOR, RETURN TO ABORT";
 1450VDU28,6,22,70,6,30
 1460*FX229,1
 1470REPEAT
 1480K%=GET
 1490IF K%=13 THEN rwr%=FALSE:GOTO 1600
 1500IF K%=135 THEN rwr%=TRUE:GOTO 1600
 1510 IF K%<32 THEN GOTO 1600
 1520IF K%=136 THEN IF (VPOS>0) OR((VPOS=0) AND(POS>0)) THEN VDU8:GOTO 1600
 1530IF K%=137 THEN IF (VPOS<15) OR((VPOS=15) AND(POS<64)) THEN VDU9:GOTO 1600
 1540IF K%=138 THEN IF VPOS<15 THENVDU10:GOTO 1600
 1550IF K%=139 THEN IF VPOS<>0 THEN VDU11:GOTO 1600
 1560REM 
 1570REM 
 1580IF (POS<47) AND(FNs_ch<>" ") THENPROCxhex
 1590IF(POS>48) AND(POS<65) THEN PROCxchr
 1600UNTIL(K%=13) OR(K%=135)
 1610IF K%=13 THEN CLS:PRINTTAB(15,8)"EDITING ABORTED"
 1620*FX229,0
 1630ENDPROC
 1640  
 1650DEFFNs_ch
 1660A%=135
 1670C%=(USR(&FFF4) AND &FFFF) DIV &100
 1680=CHR$(C%)
 1690 
 1700DEF PROCxhex
 1710IF INSTR(hex$,CHR$(K%))=0 THEN SOUND1,-15,100,8:ENDPROC
 1720cux=POS:cuy=VPOS
 1730REM
 1740VDUK%,8
 1750IF (POS MOD3)>0 THEN VDU8:REM
 1760 ldig$=FNs_ch
 1770VDU9
 1780rdig$=FNs_ch
 1790REM
 1800new%=(INSTR(hex$,ldig$)-1)*16+INSTR(hex$,rdig$)-1
 1810REM
 1820ofs%=VPOS*16+(POS DIV3)
 1830blc%?ofs%=new%
 1840REM
 1850cpos%=49+(POS DIV 3)
 1860VDU31,cpos%,VPOS:REM
 1870IF (new%<32) OR (new%>122) THEN PRINT".";:ELSE PRINTCHR$(new%);
 1880VDU31,cux,cuy
 1890VDU9
 1900ENDPROC
 1910 
 1920DEF PROCxchr
 1930cux=POS:cuy=VPOS
 1940REM
 1950ofs%=VPOS*16+POS-49
 1960blc%?ofs%=K%
 1970REM
 1980cpos%=(POS-49)*3
 1990VDU31,cpos%,VPOS
 2000IF K%>15 THEN PRINT;~K%" ";ELSE PRINT;"0";~K%;" ";
 2010VDU31,cux,cuy
 2020IF (K%<32) OR(K%>122) THEN PRINT".";:ELSE PRINTCHR$(K%);
 2030ENDPROC
 2040 
 2050DEF PROCintro
 2060COLOUR129:CLS
 2070VDU28,2,29,17,2
 2080COLOUR130:CLS
 2090VDU28,4,27,15,4
 2100COLOUR131:CLS
 2110MOVE565,850
 2120GCOL0,0
 2130PLOT1,150,0:PLOT1,0,-80:PLOT1,-150,0:PLOT1,0,80
 2140PLOT0,20,-40:PLOT1,110,0:PLOT0,-72,28:PLOT0,34,0
 2150PLOT81,0,-53:PLOT0,-34,0:PLOT81,0,53:PLOT0,-29,-48
 2160GCOL0,1
 2170PLOT65,0,0:PLOT65,4,0:PLOT65,0,4:PLOT65,-4,0
 2180COLOUR0:PRINTTAB(4,5)"DISC"'TAB(3,7)"SECTOR"'TAB(3,9)"EDITOR"
 2190PRINT'''''"DRIVE ?";:A$=GET$:D%=VAL(A$):A$=STR$(D%)
 2200ENDPROC
 2210DEF PROCcm(A$):$blc%=A$:X%=blc% MOD256:Y%=blc%DIV256:CALL&FFF7:ENDPROC

