>LIST
    5REM VERSION OF DISASSEMBLER         PRINTS IN WIDE FORMAT FOR EASY READING
   10 LL=66
   15 R3=30:R4=55
   30 *FX4,1
   35 *FX 6,0
   40 PROCsetup
   50 P%=1
   60 L%=0:H%=1
   70 PROCenter
   80 
   90 P%(P%)=S%
  100 S%=P%(P%)
  140 IF L%=0 FOR I%=1 TO LL
  150 PROCaddress
  160 M$=M$(?S%)
  170 T%=ASC(RIGHT$(M$,1))
  180 PROCopmn
  190 IF T%=76 PROCbranch:GOTO 240
  200 IF T%<66 THEN 240
  210 PROCbefore
  220 IF C%=2 PROCtwo ELSE PROCthr
  230 PROCafter
  240 PROCascii
  250 PRINT
  260 S%=S%+C%
  270 IF S%>&C000 VDU 3:END
  280 IF L%=0 NEXT
  290 PRINT'''
  300 
  310 *FX15,1
  320 K%=INKEY(0)
  330 IF K%=-1 THEN 320
  340 P%=P%+1:GOTO 90
  350 REM:IF L%=0 AND K%=139 AND P%>1 P%=P%-1:GOTO100
  360IF L%=0 AND K%=77 CLEAR:GOTO30
  370 VDU 7
  380 GOTO 310
  390 
  400 DEF PROCsetup
  410 N%=150
  420 DIM P%(N%),M$(255)
  430 FOR I%=0 TO 255
  440 READ M$(I%)
  450 NEXT
  460 ENDPROC
  470 
  480 DEF PROCenter
  490 PR%=1
  500 D%=&B05C:S%=D%
  510 ENDPROC
  520 
  530 DEF PROCblank
  540 PRINT TAB(0,Y%);
  550 FOR I%=0 TO 6
  560 PRINT STRING$(40," ");
  570 NEXT
  580 PRINT TAB(0,Y%);
  590 ENDPROC
  600 
  610 DEF PROCaddress
  620 PRINT TAB(2+H%);
  630 IF S%<16 PRINT"0";
  640 IF S%<256 PRINT"0";
  650 IF S%<4096 PRINT"0";
  670 PRINT;~S%;"     ";
  690 ENDPROC
  700 
  710 DEF PROCopmn
  720 IF T%<72 C%=2 ELSE C%=3
  730 IF T%<66 C%=1
  740 IF T%=76 C%=2
  750 FOR J%=1 TO C%
  760 B%=?(S%+J%-1)
  770 IF B%<16 PRINT"0";
  780 PRINT;~B%;SPC(1);
  790 NEXT
  810 PRINT TAB(19+R3);
  820 A$=LEFT$(M$,3)
  860 PRINTTAB(20+R3);
  870 IF T%=63 PRINT"???"; ELSE PRINT A$;
  880 IF T%=64 PRINT SPC(2);"A";
  890 ENDPROC
  900 
  910 DEF PROCbefore
  920 PRINT TAB(24+R3);
  930 IF T%=66 PRINT"#";
  940 IF T%=70 OR T%=71 OR T%=75 PRINT"(";
  950 ENDPROC
  960 
  970 DEF PROCtwo
  980 PRINT TAB(25+R3);
  990 B%=?(S%+1)
 1000 IF B%<16 AND H%=1 PRINT"0";
 1010 IF H%=1 PRINT;~B%;
 1020 IF H%=0 PRINT;B%;
 1030 ENDPROC
 1040 
 1050 DEF PROCthr
 1060 PRINT TAB(25+R3);
 1070 FOR J%=2 TO 1 STEP -1
 1080 B%=?(S%+J%)
 1090 IF J%=2 B1%=B%
 1100 IF H%=1 AND B%<16 PRINT"0";
 1110 IF H%=1 PRINT;~B%;
 1120 IF J%=1 AND H%=0 PRINT;B1%*256+B%;
 1130 NEXT
 1140 ENDPROC
 1150 
 1160 DEF PROCafter
 1170 IF T%>67 AND T%<71 PRINT",";
 1180 IF T%=73 OR T%=74 PRINT",";
 1190 IF T%=71 OR T%=75 PRINT")";
 1200 IF T%=71 PRINT",";
 1210 IF T%=68 OR T%=70 PRINT"X";
 1220 IF T%=73 PRINT"X";
 1230 IF T%=69 OR T%=74 PRINT"Y";
 1240 IF T%=70 PRINT")";
 1250 IF T%=71 PRINT"Y";
 1260 ENDPROC
 1270 
 1280 DEF PROCascii
 1290IF C%<>3 GOTO1360 ELSE Z%=B1%*256+B%
 1300RESTORE 1720:X%=0:PRINTTAB(32+R4);
 1310FORV%=1TO47
 1320READ W%,W$
 1330IF Z%=W% PRINT;W$;:X%=1:V%=47
 1340NEXT
 1350IF X%=1 ENDPROC
 1360 PRINT TAB(33+R4);
 1370 FOR J%=1 TO C%
 1380 B%=?(S%+J%-1)
 1390 IF B%>31 AND B%<127 PRINT CHR$(B%); ELSE PRINT SPC(1);
 1400 NEXT
 1410 ENDPROC
 1420 
 1430 DEF PROCbranch
 1440 PRINT TAB(25+R3);
 1450 B%=?(S%+1)
 1460 IF B%<128 Q%=S%+B%+2
 1470 IF B%>127 Q%=S%+B%-254
 1480 IF H%=1 PRINT;~Q%;
 1490 IF H%=0 PRINT;Q%;
 1500 ENDPROC
 1510 
 1520DEF FNA(x%)
 1530=(1-PR%)*x%+PR%*32
 1540 DATA BRKA,ORAF,?,?,?,ORAC,ASLC,?,PHPA,ORAB,ASL@,?,?,ORAH,ASLH
 1550 DATA ?,BPLL,ORAG,?,?,?,ORAD,ASLD,?,CLCA,ORAJ,?,?,?,ORAI,ASLI
 1560 DATA ?,JSRH,ANDF,?,?,BITC,ANDC,ROLC,?,PLPA,ANDB,ROL@,?,BITH
 1570 DATA ANDH,ROLH,?,BMIL,ANDG,?,?,?,ANDD,ROLD,?,SECA,ANDJ,?,?,?
 1580 DATA ANDI,ROLI,?,RTIA,EORF,?,?,?,EORC,LSRC,?,PHAA,EORB,LSR@
 1590 DATA ?,JMPH,EORH,LSRH,?,BVCL,EORG,?,?,?,EORD,LSRD,?,CLIA,EORJ
 1600 DATA ?,?,?,EORI,LSRI,?,RTSA,ADCF,?,?,?,ADCC,RORC,?,PLAA,ADCB
 1610 DATA ROR@,?,JMPK,ADCH,RORH,?,BVSL,ADCG,?,?,?,ADCD,RORD,?,SEIA
 1620 DATA ADCJ,?,?,?,ADCI,RORI,?,?,STAF,?,?,STYC,STAC,STXC,?
 1630 DATA DEYA,?,TXAA,?,STYH,STAH,STXH,?,BCCL,STAG,?,?,STYD,STAD
 1640 DATA STXE,?,TYAA,STAJ,TXSA,?,?,STAI,?,?,LDYB,LDAF,LDXB,?
 1650 DATA LDYC,LDAC,LDXC,?,TAYA,LDAB,TAXA,?,LDYH,LDAH,LDXH,?,BCSL
 1660 DATA LDAG,?,?,LDYD,LDAD,LDXE,?,CLVA,LDAJ,TSXA,?,LDYI,LDAI
 1670 DATA LDXJ,?,CPYB,CMPF,?,?,CPYC,CMPC,DECC,?,INYA,CMPB,DEXA,?
 1680 DATA CPYH,CMPH,DECH,?,BNEL,CMPG,?,?,?,CMPD,DECD,?,CLDA,CMPJ,?
 1690 DATA ?,?,CMPI,DECI,?,CPXB,SBCF,?,?,CPXC,SBCC,INCC,?,INXA,SBCB
 1700 DATA NOPA,?,CPXH,SBCH,INCH,?,BEQL,SBCG,?,?,?,SBCD,INCD,?,SEDA
 1710 DATA SBCJ,?,?,?,SBCI,INCI,?
 1720DATA &FFD1,OSGBPB,&FFCE,OSFIND,&FFEE,OSWRCH,&FFC8,oswrch,&FFE0,OSRDCH,&FFCB,osrdch,&FFE7,OSNEWL,&FFE3,OSASCI,&FFBC,vducoe,&FFC2,GSINIT,&FFC5,GSREAD
 1730DATA &FFB9,OSRDRM,&FFBF,OSEVEN,&FFF7,OSCLI,&FFF4,OSBYTE,&FFF1,OSWORD,&FFD4,OSBPUT,&FFD7,OSBGET,&FFDA,OSARGS,&FFDD,OSFILE
 1740DATA &0200,USERV,&0202,BRKV,&0204,IRQ1V,&0206,IRQ2V,&0208,CLIV,&020A,BYTEV,&020C,WORDV,&020E,WRCHV,&0210,RDCHV,&0212,FILEV,&0214,ARGSV,&0216,BGETV,&0218,BPUTV,&021A,GBPBV,&021C,FINDV
 1750DATA &021E,FSCV,&0220,EVENTV,&0222,UPTV,&0224,NETV,&0226,VDUV,&0228,KEYV,&022A,INSV,&022C,REMV,&022E,CNPV,&0230,IND1V,&0232,IND2V,&0234,IND3V
 1760DEF PROCerror1
 1770IF ERR<>17 CLS:PRINT'':REPORT:PRINT" at line ";ERL:PRINT'"THAT SHOULDN'T BE THERE - PLEASE"'"DEBUG ME!":END
 1780RUN
>*FX3,0
