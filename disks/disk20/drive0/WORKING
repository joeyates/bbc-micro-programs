>LIST
   10MODE7:VDU23;8202;0;0;0;
   20DIM F$(25),T$(2),W$(3,8),W%(3,8)
   30F$=FNmenu
   50PROCinput(F$)
   55PROCvars
   60PROCshuffle
   70PROCpage
  110TIME=0
  130 
  135IF FNcheck T%=T%-1:GOTO 135
  150IF H% PROCdisplay
  155IF W%=0 GOTO 270
  160PROCbullet
  180IF Y%<>T%+7 GOTO 250
  190IF FNhit PROCfill:W%=W%-1
  200PRINTTAB(X%,Y%);" ";:Y%=24
  250GOTO135
  260 
  270S$=STR$(P%+TIME/100)
  275PROCbig(4,130,131,"Your Time :"+S$)
  280R%=FNchoice
  300GOTO30
  310 
  320DEFFNcheck
  330R%=TRUE:I%=0
  340REPEATIF W%(I%,T%)<>O% R%=FALSE
  345I%=I%+1
  360UNTIL I%=4 OR R%=FALSE
  370=R%
  500 
  510DEFPROCdisplay
  520CLS
  525PROCbig(0,132,134,"LEXIS :"+T$)
  527PROCbig(2,129,131,"Penalties :"+STR$(P%))
  530FOR I%=0 TO 3:FOR J%=0 TO T%
  540PRINTTAB(I%*10,J%+6);W$(I%,J%)
  550NEXT J%,I%:H%=FALSE
  555ENDPROC
  560 
  561DEFPROCbullet
  562PRINTTAB(X%,Y%);" ";
  563IF Y%>0 AND Y%<24 Y%=Y%-1
  564IFINKEY(-65) AND X%>0  X%=X%-1
  565IFINKEY(-2)  AND X%<38 X%=X%+1
  566IFINKEY(-74) AND Y%=24 Y%=23
  568PRINTTAB(X%,Y%);"^";
  569ENDPROC
  570 
  580DEFFNhit:R%=FALSE:C%=X%/10
  610IFX%>=C%*10+LEN(W$(C%,T%)) GOTO650
  620IF W$(C%,T%)="" GOTO 650
  625H%=TRUE
  630IF W%(C%,T%)<>O% GOTO 640
  635P%=P%+1:SOUND1,-12,20,3:GOTO650
  640R%=TRUE:SOUND0,-12,30,8
  650=R%
  660 
  670DEFPROCfill:C%=X%/10
  680A%=(N%-1) MOD 4:B%=(N%-1) DIV 4
  720W$(C%,T%)=W$(A%,B%)
  725W%(C%,T%)=W%(A%,B%)
  726W$(A%,B%)="":W%(A%,B%)=0:N%=N%+1
  730ENDPROC
  930 
  940DEFFNmenu
  950CLS:RESTORE:READ T$,M%,Q$
  955PROCbig(0,132,134,"LEXIS :"+T$)
  970PRINTTAB(0,4);
  980FOR I%=1 TO M%:READ F$(I%),G$
 1000PRINT;CHR$(64+I%);")";TAB(3);G$
 1010NEXT I%
 1011PROCbig(22,131,130,"Press the LETTER of the exercise")
 1012R%=FNchoice
 1013IF R%=101 CHAIN"LEXIS"
 1015IF R%<>102 GOTO 1019
 1017*FX 210,1
 1018GOTO1012
 1019IF R%<>103 GOTO 1024
 1020*FX 210,0
 1022GOTO1012
 1024R%=(R% AND &5F)-64
 1025IF R%<1 OR R%>M% GOTO 1011
 1030F$=Q$+F$(R%)
 1040=F$
 1050 
 1060DEFPROCinput(F$)
 1070F%=OPENUP F$
 1075FORK%=0 TO 1
 1080INPUT#F%,T$(K%+1)
 1090FOR I%=0TO3:FOR J%=0 TO 3
 1100INPUT#F%,W$(I%,J%+4*K%),W%(I%,J%+4*K%)
 1110NEXT J%,I%,K%
 1120CLOSE#0
 1121ENDPROC
 1122 
 1125DEF PROCpage:CLS
 1140PROCbig(0,132,134,"LEXIS :"+T$)
 1150PROCbig(5,130,131,"Shoot")
 1153PROCbig(7,130,131,T$(3-O%))
 1156PROCbig(10,129,131,"Don't shoot")
 1159PROCbig(12,129,131,T$(O%))
 1162PROCbig(22,131,130,"  Press FIRE to start the game")
 1165R%=FNchoice
 1200ENDPROC
 1210 
 1290DEFPROCvars:W%=16:X%=18:Y%=24
 1300T%=7:N%=1:H%=TRUE:P%=0:ENDPROC
 1320 
 1330DEF FNchoice
 1340*FX 15,1
 1350REPEATR%=GET:UNTILR%<>0
 1420=R%
 1430 
 1440DEF PROCbig(B%,C%,D%,B$)
 1450FOR E%=0 TO 1:PRINTTAB(0,B%+E%);
 1460VDU C%,157,D%,141:PRINTB$
 1470NEXT E%
 1480ENDPROC
 1500 
 1510DEF PROCshuffle
 1520FOR K%=1TO20
 1530A%=RND(4)-1:B%=RND(8)-1
 1540C%=RND(4)-1:D%=RND(8)-1
 1550E%=W%(A%,B%):W%(A%,B%)=W%(C%,D%)
 1560E$=W$(A%,B%):W$(A%,B%)=W$(C%,D%)
 1570W%(C%,D%)=E%:W$(C%,D%)=E$
 1580NEXT K%
 1590O%=RND(2)
 1610ENDPROC
 1620 
 2000DATA Grammar,10,:2.G.
 2010DATA PREPAST,Present tenses and Past tenses
 2020DATA PAST/PP,Past Tenses and Past Participles
 2030DATA GER/INF,Verbs with Gerund and Infinitive
 2040DATA IRREGPL,Irregular plurals
 2050DATA WEAKFMS,Weak Forms
 2060DATA MAKE/DO,Make words and Do words
 2070DATA PRONPOS,Pronouns and Possessive Pronouns
 2080DATA MOD/AUX,Modal verbs and Auxilliary verbs
 2090DATA THE/0,Articles : 'The' or no article
 2100DATA PLCTIME,Prepositions of Place and Time
>*FX3,0
