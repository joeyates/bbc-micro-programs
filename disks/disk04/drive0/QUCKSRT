   10 N=30
   20 DIM A(N)
   30 FOR I=1 TO N
   40 A(I)=I
   50 NEXT I
   60 T=TIME
  100 M=12:DIM SL(M),SR(M)
  110 S=1:SL(1)=1:SR(1)=N
  120 L=SL(S):R=SR(S):S=S-1
  130 I=L:J=R:X=A(INT((L+R)/2))
  140 IF A(I)<X THEN I=I+1:GOTO 140
  150 IF X<A(J) THEN J=J-1:GOTO 150
  160 IF I>J THEN 190
  170 W=A(I):A(I)=A(J):A(J)=W
  180 I=I+1:J=J-1
  190 IF I<=J THEN 140
  200 IF I>=R THEN 220
  210 S=S+1:SL(S)=I:SR(S)=R
  220 R=J
  230 IF L<R THEN 130
  240 IF S<>0 THEN 120
  250 PRINT:PRINT" DONE IN ";(TIME-T)/100;" SECONDS.":PRINT 
