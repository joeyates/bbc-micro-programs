
   10 MODE 7
   20 PRINT:PRINT:PRINT
   30 INPUT "Give FUNCTION :",F$
   40 INPUT "Give x limits :",X1,X2
   50 INC=(X2-X1)/1280
   60 X=X1
   70 MAX=EVAL(F$)
   80 MIN=MAX
   90 FOR X=X1 TO X2 STEP INC*4
  100 F=EVAL(F$)
  110 IF F>MAX THEN MAX=F
  120 IF F<MIN THEN MIN=F
  130 NEXT X
  140 J=(MAX-MIN)/1000
  150 MODE 1
  160 IF X1*X2<=0 THEN PLOT 69,FNH(0),0:PLOT 5,FNH(0),1024
  170 IF MIN*MAX<=0 THEN PLOT 69,0,FNV(0):PLOT 5,1280,FNV(0)
  180 X=X1:F=EVAL(F$):PLOT 69,0,FNV(F)
  190 FOR X=X1 TO X2 STEP INC 
  200 F=EVAL(F$)
  210 PLOT 5,FNH(X),FNV(F)
  220 NEXT X
  225 Y$=INKEY$(0):IF Y$="" THEN 225
  228 MODE 7:PRINT:PRINT:END
  230 DEF FNH(X)=(X-X1)/INC
  240 DEF FNV(Y)=(Y-MIN)/J

