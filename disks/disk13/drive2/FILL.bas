
   10 MODE 1
   20 PLOT 69,700,250
   30 FOR T=0 TO 2.02*PI STEP .1
   40 PLOT 5,350*(COS(T)+1),250*(SIN(T)+1)
   50 NEXT T
   60 PLOT 69,690,360
   70 FOR T=0 TO 2*PI+.1 STEP .1
   80 PLOT 5,300*(COS(T)+1.3),200*(SIN(T)+1.8)
   90 NEXT T
  100 DIM A%(100,2)
  110 S%=0:PROCPUSH(200,490)
  120 GCOL 0,1
  130 PROCFILL
  140 PROCPUSH(150,300)
  150 GCOL 0,2
  160 PROCFILL
  170 END
  180 DEFPROCFILL
  190 REPEAT
  200 X%=A%(S%,1):Y%=A%(S%,2):S%=S%-1
  210 REPEAT
  220 Y%=Y%+4
  230 UNTIL POINT(X%,Y%)
  240 Y%=Y%-4
  250 IF POINT(X%+4,Y%)=0 THEN PROCPUSH(X%+4,Y%)
  260 IF POINT(X%-4,Y%)=0 THEN PROCPUSH(X%-4,Y%)
  270 PLOT 69,X%,Y%
  280 REPEAT
  290 IF POINT(X%+4,Y%)=0 AND POINT(X%+4,Y%+4) THEN PROCPUSH(X%+4,Y%)
  300 IF POINT(X%-4,Y%)=0 AND POINT(X%-4,Y%+4) THEN PROCPUSH(X%-4,Y%)
  310 Y%=Y%-4
  320 UNTIL POINT(X%,Y%-4)
  330 PLOT 5,X%,Y%
  340 UNTIL S%=0
  350 ENDPROC
  360 
  370 DEF PROCPUSH(J%,K%)
  380 IF A%(S%,1)=J% AND A%(S%,2)=K% THEN 420
  390 S%=S%+1
  400 A%(S%,1)=J%
  410 A%(S%,2)=K%
  420 ENDPROC

