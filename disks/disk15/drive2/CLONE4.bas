>LIST
   10DIM data% 4097,ID% 200,bl% 20
   20X%=bl% MOD 256:Y%=bl% DIV 256
   30MODE7:A%=&7F:osw%=&FFF1:?bl%=0
   40REM   SEEK TRACK 0
   50PROCOSW(0,0,1,&69,0,0,0,0,0)
   60IF bl%?8<>0 THEN END
   70SKIP=FALSE:FOR tr%=0 TO 79
   80IF SKIP THEN 100
   90PRINT'"SOURCE:";:REPEATUNTILGET=32
  100REM   READ SECTOR IDS
  110PROCOSW(0,ID%,3,&5B,tr%,0,18,0,0)
  120SKIP=FALSE
  130IF bl%?10<>0 SKIP=TRUE:GOTO 340
  140PROCHOWMANY:PROCLOWEST
  150REM   PROGRAM 8271 FOR SOFT TRACK
  160PROCOSW(0,0,2,&3A,&12,?ID%,0,0,0)
  210REM   READ TRACK
  220PROCOSW(0,data%,3,&57,?ID%,low%,32*ID%?3+NUM%+1,0,0)
  230CLS:PRINT"Track ";tr%
  240del%=bl%?10 AND &20:err%=bl%?10 AND &1F
  250IF err%<>0 THEN PRINT"###";err%
  260PRINT'"DEST:";:REPEATUNTILGET=32
  270REM   FORMAT TRACK
  280PROCOSW(0,ID%,5,&63,?ID%,21,32*ID%?3+NUM%+1,0,16)
  290REM   WRITE DATA
  300IFdel%=0 wr%=&4B ELSE wr%=&4F
  310PROCOSW(0,data%,3,wr%,?ID%,low%,32*ID%?3+NUM%+1,0,0)
  320REM   REPROG 8271 FOR PHYSICAL TRK
  330PROCOSW(0,0,2,&3A,&12,tr%,0,0,0)
  340NEXT tr%:END
  350DEFPROCOSW(side%,addr%,par%,comm%,P1%,P2%,P3%,P4%,P5%)
  360bl%?0=side%:bl%!1=addr%
  370bl%?5=par%:bl%?6=comm%
  380bl%?7=P1%:bl%?8=P2%:bl%?9=P3%
  390bl%?10=P4%:bl%?11=P5%
  400CALLosw%
  410ENDPROC
  420DEF PROCHOWMANY
  430NUM%=0:FIRST%=ID%?2
  440IFID%?(6+4*NUM%)<>FIRST% NUM%=NUM%+1:GOTO 440 ELSE ENDPROC
  445DEF PROCLOWEST
  450low%=&FF:FOR I=0 TO NUM%
  460IF ID%?(I*4+2)<low% low%=ID%?(I*4+2)
  470NEXTI:ENDPROC
>*FX3,0
